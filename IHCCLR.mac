|- IHCCLR.mac - By IHC385
|- Automatic Cleric bot -
|- Modded Version of AutoCleric (Auto_Clr) with expanded features 
|- Version 2.0
|- Updated: 4/25/2019
|- Credit: Noobhax0r, Maskoi, Ctaylor, Treehuggindruid. A lot of ideas were borrowed and modified from their macros
#Chat group
#Chat tell
#Chat raid
#turbo 80
#include IHCMercs\ihcsubs.inc
#include IHCMercs\ihcclrutil.inc
#include IHCMercs\ihcevents.inc
#warning
#bind SetVarInt	/setvarint
#bind CmdList   /ihchelp
#bind CureOff	/cureoff
#bind BNDGroupCure	/curegroup
#Event Buff 			"Your #1# spell has worn off of #*#."
#Event GoMana			"You have been granted a gift of#*#"
#Event GoMoff			"Your gift of#*#mana fades."
#Event ReactCure		"#*#strangled by a suffocating breeze#*#"

Sub Main
	/delay ${Math.Rand[3]}
	/doevents flush
	/declare MacroName       	string outer IHCCLR
	/declare MacroPath       	string outer IHCMercs\IHCCLR
	/declare IHCVersion       	string outer 2.0 
	/declare IniFileName     	string outer ${MacroPath}_${EverQuest.Server}_${Me.CleanName}.ini

	/call VarSetup
	/call ClrSetup	
	/call AliasSetup
	/call CheckSpells
	/call SetControlToon
	/call LoadSpellBar
	/squelch /alert clear 3
	/squelch /alert clear 4
|-Cursor ID
	/if (${Cursor.ID}) {
		/echo Sending Item on Cursor to Bag
		/autoinventory
	}
	/call WelcomeMsg
	/if (${assistid}==${Me.ID}) {
		/echo \aw I am Main Assist
	} else {
		/echo \aw Assisting \ay >> \ag ${assistname} \ay << \aw at \ag ${AutoAssistAt}%
	}

	/if (${UseZoneLogic}) {
		/call ZoneEventChk
	}

 :mainloop
	/call ModeSet
	/if (${Mode}==0) /call Travel
	/if (${Mode}==1) /call ManualPullCamping
	/if (${Mode}==2) /call ManualRunAndGun
	/if (${Mode}==3) /call ManualPullCamping
	/if (${Mode}==4) /call AFKRunAndGun
	/if (${Mode}==5) /call DoEvent
/goto :mainloop
/return
|----------------------------------------------------------------------------
|- SUB: ManualRunAndGun
|----------------------------------------------------------------------------
Sub ManualRunAndGun
	/if (${MakeCamp.Status.Equal[ON]}) /makecamp off
	/call RemoveTravelBuffs
	:runandgun
	/if (${Me.Hovering}) /call DeathSub
	/if (${Me.State.Equal[FEIGN]}) /stand


	/if (!${Me.XTarget[1].ID} && !${Me.Moving}) /varset RunAndGunMedTimer 7s
	/if (!${Me.XTarget[1].ID} && !${Me.Casting.ID} && ${RunAndGunMedTimer}==0) /call IllusionCheck
	/if (!${Me.Moving} && !${Me.XTarget[1].ID} && !${Target.ID} && !${Me.Sitting} && ${RunAndGunMedTimer}==0) /sit
	/if (${Me.PctMana}>=30 && !${Me.Casting.ID} && !${Me.XTarget[1].ID} && !${Me.Moving} && ${RunAndGunMedTimer}==0) /call BuffGroup
	/if (${Me.PctMana}>=30 && !${Me.Casting.ID} && !${Me.XTarget[1].ID} && !${Me.Moving} && ${RunAndGunMedTimer}==0) /call BuffSelf
	
	/call TankChangeCheck

	/if (${Me.XTarget[1].ID}) /call CastersEngage
	/if (${Me.XTarget[1].ID} && ${Target.ID} && ${Target.Type.Equal[NPC]}) /call MoveToCamp ${Target.Y} ${Target.X} ${Target.Z}


	/if (${Cursor.ID}) /call BagModRods
	/call TankChangeCheck
	/if (!${Me.Combat} && !${Me.XTarget[1].ID} && !${Me.Invis} && !${Me.Casting.ID} && !${Window[SpellBookWnd]}) /call IllusionCheck
	/if (${DynamicHealing}) /call ConfigHeal
	/call ExtraHeal
	/if (${Me.Song["Touch of the Divine"].ID}) /call DALogic
	/if ((${Me.PctAggro}>90||${Me.XTarget[2].PctAggro}>90 || ${Me.XTarget[3].PctAggro}>90) && (${Me.PctHPs}<=${grouphealpoint}) && (!${Me.Buff[Sanctuary].ID})) /call AggroCheck
	/if (!${Me.Casting.ID} && (${Me.XTarget[1].ID} || !${Me.Invis})) /call HealXT
	/if (!${Me.Casting.ID} && (${Me.XTarget[1].ID} || !${Me.Invis})) /call HealGroup
	/if ((${AutoRez}==1) && (!${Me.Moving}) && (!${Me.Invis})) /call RezCheck

	/if ((${Me.PctMana}>=30) && (!${Me.Casting.ID}) && (!${Me.XTarget[1].ID}) && (!${Me.Invis}) && !${MoveTo.Moving} && !${Group.Member[${assistname}].Hovering} && ${Group.Member[${assistname}].Type.NotEqual[Corpse]} && ${Group.Member[${assistname}].PctHPs}>=1 && ${DoBuffs}) /call BuffCheck
	/if ((${Me.PctMana}>=30) && (!${Me.Casting.ID}) && (!${Me.XTarget[1].ID}) && (!${Me.Invis}) && !${MoveTo.Moving} && (!${AdvPath.Following}) && ${groupbufftimer}==0 && ${DoBuffs}) /call BuffGroup


	/if (!${Me.XTarget[1].ID}) /call Travel

	/if (${Target.Type.Equal[Corpse]}) /target clear

	/if (!${Me.XTarget[1].ID}) /call PostCombat

	/doevents
	/if (${Mode}==2) /goto :runandgun
	/if (${Mode}!=2) /return
|----------------------------------------------------------------------------
|- SUB: ManualPullCamping
|---------------------------------------------------------------------------- 
Sub ManualPullCamping
	/call MakeCamp
    /call RemoveTravelBuffs 
	:manualpullcamping
	/if (${AdvPath.Following}) /afollow off
	/if (${Me.Hovering}) /call DeathSub
	/if (${Me.State.Equal[FEIGN]}) /stand

	/if (!${Me.XTarget[1].ID} && !${Me.Casting.ID}) /call IllusionCheck
	/if (${DoMed}) /call AutoMed

	/if (${Cursor.ID}) /call BagModRods
	/call TankChangeCheck
	/if (!${Me.Combat} && !${Me.XTarget[1].ID} && !${Me.Invis} && !${Me.Casting.ID} && !${Window[SpellBookWnd]}) /call IllusionCheck
	/if (${DynamicHealing}) /call ConfigHeal
	/call ExtraHeal
	/if (${Me.Song["Touch of the Divine"].ID}) /call DALogic
	/if ((${Me.PctAggro}>90||${Me.XTarget[2].PctAggro}>90 || ${Me.XTarget[3].PctAggro}>90) && (${Me.PctHPs}<=${grouphealpoint}) && (!${Me.Buff[Sanctuary].ID})) /call AggroCheck
	/if (!${Me.Casting.ID} && (${Me.XTarget[1].ID} || !${Me.Invis})) /call HealXT
	/if (!${Me.Casting.ID} && (${Me.XTarget[1].ID} || !${Me.Invis})) /call HealGroup
	/if ((${AutoRez}==1) && (!${Me.Moving}) && (!${Me.Invis})) /call RezCheck

	/if ((${Me.PctMana}>=30) && (!${Me.Casting.ID}) && (!${Me.XTarget[1].ID}) && (!${Me.Invis}) && !${MoveTo.Moving} && !${Group.Member[${assistname}].Hovering} && ${Group.Member[${assistname}].Type.NotEqual[Corpse]} && ${Group.Member[${assistname}].PctHPs}>=1 && ${DoBuffs}) /call BuffCheck
	/if ((${Me.PctMana}>=30) && (!${Me.Casting.ID}) && (!${Me.XTarget[1].ID}) && (!${Me.Invis}) && !${MoveTo.Moving} && (!${AdvPath.Following}) && ${groupbufftimer}==0 && ${DoBuffs}) /call BuffGroup

	/if (!${Me.Casting.ID}) /call MoveToCamp ${PullAutoCampX} ${PullAutoCampY} ${PullAutoCampZ} 10

	/doevents

	/if (${Mode}==1) /goto :manualpullcamping
	/if (${Mode}!=1) /return
|----------------------------------------------------------------------------
|- SUB: ExtraHeal - Extra Actions to Perform
|---------------------------------------------------------------------------- 
Sub ExtraHeal
	/if ((${Me.XTarget}>${BurnMobCount} || ${Me.XTarget[1].Named}) && !${Me.Casting.ID} && ${extrahealcasttime}==0 && (${Me.XTarget[1].ID}) && ${Me.XTarget[1].Distance}<=100) {
		/varset extrahealcasttime 20
|---First Line of Defence
		/if (${Me.AltAbilityReady[Fundament: Third Spire of Divinity]} && ${Spell[${Me.AltAbility[Fundament: Third Spire of Divinity]}].Stacks}) {
			/call AANow ${Me.AltAbility[Fundament: Third Spire of Divinity].ID}
		} else /if (${Me.AltAbilityReady[Healing Frenzy]} && ${Spell[${Me.AltAbility[Healing Frenzy]}].Stacks}) {
			/call AANow ${Me.AltAbility[Healing Frenzy].ID}
		} else /if (!${Me.Buff[Healing Frenzy].ID} && ${Me.AltAbilityReady[Celestial Rapidity]} && ${Spell[${Me.AltAbility[Celestial Rapidity]}].Stacks}) {
			/call AANow ${Me.AltAbility[Celestial Rapidity].ID}
		} else /if (!${Me.Buff[Third Spire of Divinity].ID} && ${Me.AltAbilityReady[Celestial Regeneration]} && ${Spell[${Me.AltAbility[Celestial Regeneration]}].Stacks}) {
			/call AANow ${Me.AltAbility[Celestial Regeneration].ID}
|---Second Ability to go with First Line
		} else /if (!${Me.Buff[Channeling the Divine].ID} && ${Me.AltAbilityReady[Improved Twincast]} && ${Spell[${Me.AltAbility[Improved Twincast]}].Stacks}) {
			/call AANow ${Me.AltAbility[Improved Twincast].ID}
		} else /if (!${Me.Buff[Improved Twincast].ID} && ${Me.AltAbilityReady[Channeling the Divine]} && ${Spell[${Me.AltAbility[Channeling the Divine]}].Stacks}) {
			/call AANow ${Me.AltAbility[Channeling the Divine].ID}
		} else /if (${Me.AltAbilityReady[Flurry of Life]} && ${Spell[${Me.AltAbility[Flurry of Life]}].Stacks}) {
			/call AANow ${Me.AltAbility[Flurry of Life].ID}
		}
	}
	/if (${Group} && ${Window[MMGW_ManageWnd].Child[MMGW_SuspendButton].Enabled} && ${Mercenary.State.Equal[DEAD]}) {
		/notify MMGW_ManageWnd MMGW_SuspendButton LeftMouseUp
		/delay 2s ${Spawn[mercenary ${Group.Member[1].ID}].ID}
	}
	/if ((${MoveTo.Moving} || ${Stick.Status.Equal[ON]} || ${AdvPath.Following}) && ${Me.Buff[Selo].ID} && ${Me.Mount.ID} && !${Me.XTarget[1].ID}) /dismount
	/if (${UseMelee}==1 && ${Me.Mount.ID}) {
		/dismount
		/if (${FindItem[${mountitem}].ID}) {
			/varset mountitem NULL
			/echo You are set to melee clearing out mountitem. Remember to use mountitem toggle to set mount back up.
		}
	}
/return
|----------------------------------------------------------------------------
|- SUB: ConfigHeal
|---------------------------------------------------------------------------- 
Sub ConfigHeal
	/if (${Select[${Zone.ID},795,796]}==0 && !${Raid.Members}) {
		/if (${Me.XTarget}>2 || ${Me.XTarget[1].Named}) {
			/if (${tankhealpoint}!=95) {
				/varset tankhealpoint 95
				/varset grouphealpoint 85
				/varset bighealpoint 80
			}
		} else /if (${Me.XTarget[1].Level}>=${Me.Level}) {
			/if (${tankhealpoint}!=85) {
				/varset tankhealpoint 85
				/varset grouphealpoint 80
				/varset bighealpoint 70
			}
		} else /if (${Me.XTarget[1].Level}>=${Math.Calc[${Me.Level}-5]}) {
			/if (${tankhealpoint}!=80) {
				/varset tankhealpoint 80
				/varset grouphealpoint 75
				/varset bighealpoint 60
			}
		} else {
			/if (${tankhealpoint}!=70) {
				/varset tankhealpoint 70
				/varset grouphealpoint 75
				/varset bighealpoint 60
			}
		}
	} else /if (${tankhealpoint}!=${tbmhealpoint} && !${Raid.Members}) {
		/varset healpoint ${tbmhealpoint}
		/varset tankhealpoint ${tbmhealpoint}
		/varcalc grouphealpoint ${tbmhealpoint}-5
		/varcalc bighealpoint ${tbmhealpoint}-10
	} else /if (${tankhealpoint}!=${raidhealpoint} && ${Raid.Members}) {
		/varset healpoint ${raidhealpoint}
		/varset tankhealpoint ${raidhealpoint}
		/varcalc grouphealpoint ${raidhealpoint}-5
		/varcalc bighealpoint ${raidhealpoint}-10
	}
/return
|----------------------------------------------------------------------------
|- SUB: Group HealQueue - New logic
|---------------------------------------------------------------------------- 
Sub HealQueue(int nextid)
	/declare nexthealspell		string local NULL
	/varset GrPCond .
	/if (${Spawn[${nextid}].Type.Equal[Pet]}) {
		/declare groupmembertoheal	string local ${Spawn[${nextid}].Master.Name}
		/varset GrPCond .Pet.
	} else {
		/declare groupmembertoheal	string local ${Spawn[${nextid}].Name}
		/varset GrPCond .
	}
|-Check to see if we can heal
	/if (${Group.Member[${groupmembertoheal}]${GrPCond}FeetWet}!=${Me.FeetWet}) /return
	/if (${Group.Member[${groupmembertoheal}]${GrPCond}Hovering} || ${Group.Member[${groupmembertoheal}]${GrPCond}Type.Equal[Corpse]}) /return
	|-/if (${Me.Buff[Withering Physicality XIII].ID} && ${Me.Buff[Withering Faith XIII].ID} && ${Me.AltAbilityReady[Purify Soul]}) /call AANow ${Me.AltAbility[Purify Soul].ID} ${Me.ID}
	|-/if (${Me.Buff[Withering Physicality XIII].ID} && ${Me.Buff[Withering Faith XIII].ID} && ${Me.AltAbilityReady[Purified Spirits]}) /call AANow ${Me.AltAbility[Purified Spirits].ID} ${Me.ID}
|-Cleric AAs before heals
	/if (${nextid}==${Me.ID} && ${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${grouphealpoint} && (${Me.AltAbilityReady[Divine Retribution]})) /call AANow 392
	/if (${nextid}==${assistid} && ${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}>${bighealpoint} && (${Me.XTarget}>2 || ${Spawn[id ${Me.XTarget[1].ID} radius 100].Named})) {
		/if (${Me.AltAbilityReady[Divine Guardian]}) /call AANow ${Me.AltAbility[Divine Guardian].ID} ${assistid}
		/if (${Me.AltAbilityReady[Focused Celestial Regeneration]}) /call AANow ${Me.AltAbility[Focused Celestial Regeneration].ID} ${assistid}
	}
|-Determine which heal to use
	/if ((${Me.SpellReady[${patchheal2}]}) && ${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${patchheal2point} && ${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}>=1 && ${Me.XTarget}>=1 && ${Me.CurrentMana}>${Spell[${patchheal2}].Mana}) {
			/varset nexthealspell ${patchheal2}
		} else /if ((${Me.AltAbilityReady[Burst of Life]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${clickhealpoint}) && ${ClickHealTimer}==0 && ${Me.XTarget}>=1) {
			/varset ClickHealTimer 20
			/call AANow ${Me.AltAbility[Burst of Life].ID} ${nextid}
		} else /if ((${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${clickhealpoint}) && (${Me.AltAbilityReady[Divine Arbitration]}) && ${ClickHealTimer}==0 && ${Group.Member[${groupmembertoheal}]${GrPCond}Type.NotEqual[Pet]} && ${Me.XTarget}>=1) {
			/varset ClickHealTimer 20
			/call AANow ${Me.AltAbility[Divine Arbitration].ID}
			/if (${Me.AltAbilityReady[Celestial Regeneration]}) /call AANow ${Me.AltAbility[Celestial Regeneration].ID}
		} else /if (${FindItem[${epicshield}].ID} && ${FindItem[${epicshield}].Timer}==0 && ${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${clickhealpoint} && ${ClickHealTimer}==0 && ${Group.Member[${groupmembertoheal}]${GrPCond}Type.NotEqual[Pet]} && ${Me.XTarget}>=1) {
			/varset ClickHealTimer 20
			/call ItemNow "${epicshield}"
		} else /if ((${Me.SpellReady[${patchheal}]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${patchhealpoint}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}>=1) && ${Me.XTarget}>=1 && ${Me.CurrentMana}>${Spell[${patchheal}].Mana}) {
			/varset nexthealspell ${patchheal}
		} else /if ((${Me.SpellReady[${wardspell}]}) && ${Me.Level}>=97 && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${bighealpoint}) && ${Group.Member[${groupmembertoheal}]${GrPCond}Type.NotEqual[Pet]} && ${Me.XTarget}>=1 && !${Target.Buff[${wardspell}].ID} && ${hottimer}==0 && ${Me.CurrentMana}>${Spell[${wardspell}].Mana}) {
			/varset nexthealspell ${wardspell}
			/varcalc hottimer ${Spell[${wardspell}].Duration.TotalSeconds}*${DurationMod}*10
		} else /if ((${Me.SpellReady[${healnuke1}]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${bighealpoint}) && ${Me.CurrentMana}>${Spell[${healnuke1}].Mana}) {
			/varset nexthealspell ${healnuke1}
		} else /if ((${Me.SpellReady[${healnuke2}]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${bighealpoint}) && ${Me.CurrentMana}>${Spell[${healnuke2}].Mana}) {
			/varset nexthealspell ${healnuke2}
		} else /if ((${Me.SpellReady[${bigheal1}]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${bighealpoint}) && ${Me.CurrentMana}>${Spell[${bigheal1}].Mana} && ${NoCures}!=1) {
			/varset nexthealspell ${bigheal1}
		} else /if ((${Me.SpellReady[${bigheal2}]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${bighealpoint}) && ${Me.CurrentMana}>${Spell[${bigheal2}].Mana} && ${NoCures}!=1) {
			/varset nexthealspell ${bigheal2}
		} else /if ((${Me.SpellReady[${remedyheal1}]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${healpoint}) && !${remedyheal1.Equal[Complete Heal]} && ${Me.CurrentMana}>${Spell[${remedyheal1}].Mana}) {
			/varset nexthealspell ${remedyheal1}
		} else /if ((${Me.SpellReady[${remedyheal1}]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${healpoint}) && ${remedyheal1.Equal[Complete Heal]} && ${Select[${Group.Member[${groupmembertoheal}]${GrPCond}Class.ShortName},WAR,SHD,PAL]}>0 && ${Me.CurrentMana}>${Spell[${remedyheal1}].Mana}) {
			/varset nexthealspell ${remedyheal1}
		} else /if ((${Me.SpellReady[${remedyheal2}]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${healpoint}) && ${Me.CurrentMana}>${Spell[${remedyheal2}].Mana}) {
			/varset nexthealspell ${remedyheal2}
		} else /if ((${Me.SpellReady[${healnuke1}]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${healpoint}) && (${nextid}==${assistid}) && ${Me.CurrentMana}>${Spell[${healnuke1}].Mana}) {
			/varset nexthealspell ${healnuke1}
		} else /if ((${Me.SpellReady[${healnuke2}]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${healpoint}) && (${nextid}==${assistid}) && ${Me.CurrentMana}>${Spell[${healnuke2}].Mana}) {
			/varset nexthealspell ${healnuke2}
		} else /if ((${Me.SpellReady[${bigheal1}]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${healpoint}) && (${nextid}==${assistid}) && ${Me.CurrentMana}>${Spell[${bigheal1}].Mana} && ${NoCures}!=1) {
			/varset nexthealspell ${bigheal1}
		} else /if ((${Me.SpellReady[${bigheal2}]}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${healpoint}) && (${nextid}==${assistid}) && ${Me.CurrentMana}>${Spell[${bigheal2}].Mana} && ${NoCures}!=1) {
			/varset nexthealspell ${bigheal2}
		} else /if ((${Me.SpellReady[${nukeheal1}]}) && (${Spawn[NPC los ${Me.XTarget[1].Name}].Distance}<150) && (${Me.XTarget[1].PctHPs}<90) && (${Me.XTarget[1].PctHPs}>10) && ${Me.PctAggro}<50 && (${Me.PctMana}>=${Manatonuke}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${healpoint}) && ${Me.CurrentMana}>${Spell[${nukeheal1}].Mana}) {
			/call SpellQueue "${nukeheal1}" ${Me.XTarget[1].ID}
		} else /if ((${Me.SpellReady[${nukeheal2}]}) && (${Spawn[NPC los ${Me.XTarget[1].Name}].Distance}<150) && (${Me.XTarget[1].PctHPs}<90) && (${Me.XTarget[1].PctHPs}>10) && ${Me.PctAggro}<50 && (${Me.PctMana}>=${Manatonuke}) && (${Group.Member[${groupmembertoheal}]${GrPCond}PctHPs}<=${healpoint}) && ${Me.CurrentMana}>${Spell[${nukeheal2}].Mana}) {
			/call SpellQueue "${nukeheal2}" ${Me.XTarget[1].ID}
		}
	/if (${Bool[${nexthealspell}]}) {
		|------------Broadcast Spell--------------------
		/if (${Me.Sitting}) /stand
		/if (${Window[SpellBookWnd]}) /book
		/if (${Target.ID}!=${nextid}) {
			/if (${Me.Combat}) /squelch /attack off
			/target id ${nextid}
			/delay 5 ${Target.ID}==${nextid}
		}
		/if (!${Me.Casting.ID} && ${Me.SpellReady[${nexthealspell}]}) {
			/cast "${nexthealspell}"
			/if (${acverbose}!=0 && ${Me.Casting.ID}==${Spell[${nexthealspell}].ID}) {
				/if (${Target.PctHPs}>=85) {
|-					/docommand /dgt \ag ${nexthealspell} \ar =>> \ay ${groupmembertoheal} \ar <<= \ag ${Target.PctHPs}% HP \ax
				} else /if (${Target.PctHPs}>=50) {
|-					/docommand /dgt \ag ${nexthealspell} \ar =>> \ay ${groupmembertoheal} \ar <<= \ay ${Target.PctHPs}% HP \ax
				} else /if (${Target.PctHPs}<50) {
|-					/docommand /dgt \ag ${nexthealspell} \ar =>> \ay ${groupmembertoheal} \ar <<= \ar ${Target.PctHPs}% HP \ax
				}
			}
			/varset groupbufftimer 1m
			/delay 10 ${Window[CastingWindow].Open}
			/delay ${Math.Calc[(${Spell[${nexthealspell}].MyCastTime}/100)+5].Int} !${Me.Casting.ID}
		}
|-		/if (${Cast.Result.Equal[CAST_SUCCESS]}) {
|-			/if ((!${EQBC.Connected} || !${Bool[${Plugin[MQ2EQBC]}]}) && ${acverbose}!=0) /docommand /echo [+o+]${nexthealspell} =>> ${groupmembertoheal} <<=[+x+]
|-			/if (${EQBC.Connected} && ${acverbose}!=0) /docommand /bc [+o+]${nexthealspell} =>> ${groupmembertoheal} <<=[+x+]
|-		}
	}
	/varset GrPCond .
/return
|----------------------------------------------------------------------------
|- SUB: BuffGroup
|---------------------------------------------------------------------------- 
Sub BuffGroupOLD
	/declare bmember			int local 0
	/if (${groupbufftimer}==0) /varset groupbufftimer 1m
	/for bmember 0 to ${Group}
		/if (${Group.Member[${bmember}].Distance}<100) {
			/target id ${Group.Member[${bmember}].ID}
			/delay 5 ${Target.ID}==${Group.Member[${bmember}].ID}
			/if (${SpawnCount[npc radius ${AssistRange} zradius 15 targetable playerstate 4]} || ${Me.XTarget[1].ID}) /break
			/delay 2s 
			/if (${Target.ID} && ${Target.BuffsPopulated}==TRUE && !${Target.Aego.ID} && !${Target.Skin.ID} && !${Target.Symbol.ID} && !${Target.Buff[${GroupBuff}].ID} && ${GroupBuffRecast}!=1) /varset GroupBuffRecast 1
		}
		/if (${SpawnCount[npc radius ${AssistRange} zradius 15 targetable playerstate 4]} || ${Me.XTarget[1].ID}) /break
	/next bmember
	/if (${assistid}) {
		/target id ${assistid}
	} else {
		/target id ${Me.ID}
	}
/return
|----------------------------------------------------------------------------
|- SUB: BuffGroup
|---------------------------------------------------------------------------- 
Sub BuffGroup
	/declare bmember			int local 0
	/if (${groupbufftimer}==0) /varset groupbufftimer 10s
	/for bmember 1 to ${Group}
		/if (${Group.Member[${bmember}].Distance}<100 && !${Group.Member[${bmember}].OtherZone} && (!${Me.Casting.ID}) ) {

			/call Gquery ${EverQuest.Server.Lower}_${Group.Member[${bmember}].Name.Lower} "Me.Aego.ID" "tmpbuffid" "int"
			/call Gquery ${EverQuest.Server.Lower}_${Group.Member[${bmember}].Name.Lower} "Me.Skin.ID" "tmpskinid" "int"
			/call Gquery ${EverQuest.Server.Lower}_${Group.Member[${bmember}].Name.Lower} "Me.Symbol.ID" "tmpsymbolid" "int"
			/call Gquery ${EverQuest.Server.Lower}_${Group.Member[${bmember}].Name.Lower} "Me.Aego.ID" "tmpaegoid" "int"
			/call Gquery ${EverQuest.Server.Lower}_${Group.Member[${bmember}].Name.Lower} "Spell[${GroupBuff}].Stacks" "tmpstackcheck" "bool"
			/if (${tmpstackcheck} && !${tmpbuffid} && ${Me.Book[${GroupBuff}]} && ${Me.CurrentMana}>${Spell[${GroupBuff}].Mana} && (!${Me.Casting.ID})) {
				/call SpellQueue "${GroupBuff}" ${Group.Member[${bmember}].ID}
				/varset spellrememtimer 5s
				/delay 10
			}





				/if (${Me.CurrentMana}>${Spell[${TankBuff}].Mana} && ${Group.Member[${bmember}].ID}==${assistid} && (!${Me.Casting.ID}) && ${Me.Book[${TankBuff}]}) {
					/call Gquery ${EverQuest.Server.Lower}_${Group.Member[${bmember}].Name.Lower} "Me.Buff[${TankBuffMatch}].ID" "tmpbuffid" "int"
					/call Gquery ${EverQuest.Server.Lower}_${Group.Member[${bmember}].Name.Lower} "Spell[${TankBuff}].Stacks" "tmpstackcheck" "bool"
					/if (!${tmpbuffid} && ${tmpstackcheck}) {
						/call SpellQueue "${TankBuff}" ${Group.Member[${bmember}].ID}
						/varset spellrememtimer 5s
						/delay 10
					}
				}
				/if (${Me.CurrentMana}>${Spell[${DivineBuff}].Mana} && ${Group.Member[${bmember}].ID}==${assistid} && (!${Me.Casting.ID}) && ${Me.SpellReady[${DivineBuff}]} && ${DivineBuffOn}) {
					/call Gquery ${EverQuest.Server.Lower}_${Group.Member[${bmember}].Name.Lower} "Me.Buff[${DivineBuff}].ID" "tmpbuffid" "int"
					/call Gquery ${EverQuest.Server.Lower}_${Group.Member[${bmember}].Name.Lower} "Spell[${DivineBuff}].Stacks" "tmpstackcheck" "bool"
					/if (!${tmpbuffid} && ${tmpstackcheck}) {
						/call DivineUsage
						/varset spellrememtimer 5s
						/delay 10
					}
				}
		}
	/next bmember
	|- Delete variable's
	/if (${Defined[tmpbuffid]}) /deletevar tmpbuffid
	/if (${Defined[tmpskinid]}) /deletevar tmpskinid
	/if (${Defined[tmpsymbolid]}) /deletevar tmpsymbolid
	/if (${Defined[tmpaegoid]}) /deletevar tmpaegoid
	/if (${Defined[tmpstackcheck]}) /deletevar tmpstackcheck
	/if (${Spawn[id ${assistid}].ID} && ${Target.ID}!=${assistid}) {
		/target id ${assistid}
	} else /if (!${Spawn[id ${assistid}].ID}) {
		/target id ${Me.ID}
	}
/return
|----------------------------------------------------------------------------
|- SUB: HealXT
|---------------------------------------------------------------------------- 
Sub HealXT
	/declare emember			int local 0
	/declare worsthurtid		int local 0
	/declare worsthp			int local 100
	/declare worsthurtname		string local NULL
	/declare nexthealspell		string local NULL	
	/for emember 1 to 10
		/if (${Me.XTarget[${emember}].Type.Equal[PC]} && !${Me.XTarget[${emember}].Hovering} && ${Me.XTarget[${emember}].Type.NotEqual[Corpse]}) {
			/if (${Me.XTarget[${emember}].Distance}<=200 && ${Me.XTarget[${emember}].PctHPs}<=${worsthp} && ${Me.XTarget[${emember}].PctHPs}>=1) {
				/varset worsthurtid ${Me.XTarget[${emember}].ID}
				/varset worsthurtname ${Me.XTarget[${emember}].Name}
				/varset worsthp ${Me.XTarget[${emember}].PctHPs}
			}
		}
	/next emember
	/if (${worsthp}<=${grouphealpoint}) {
		/if ((${Me.SpellReady[${patchheal2}]}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${patchheal2point}) && (${Me.XTarget[${worsthurtname}].PctHPs}>=1) && ${Me.XTarget}>=1 && ${Me.CurrentMana}>${Spell[${patchheal2}].Mana}) {
			/varset nexthealspell ${patchheal2}
		} else /if ((${Me.AltAbilityReady[Burst of Life]}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${clickhealpoint}) && ${ClickHealTimer}==0 && ${Me.XTarget}>=1) {
			/varset ClickHealTimer 20
			/call AANow ${Me.AltAbility[Burst of Life].ID} ${worsthurtid}
		} else /if ((${Me.SpellReady[${patchheal}]}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${patchhealpoint}) && (${Me.XTarget[${worsthurtname}].PctHPs}>=1) && ${Me.XTarget}>=1 && ${Me.CurrentMana}>${Spell[${patchheal}].Mana}) {
			/varset nexthealspell ${patchheal}
		} else /if ((${Me.SpellReady[${wardspell}]}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${bighealpoint}) && ${Me.XTarget[${worsthurtname}].Type.NotEqual[Pet]} && ${Me.XTarget}>=1 && !${Target.Buff[${wardspell}].ID} && ${hottimer}==0 && ${Me.CurrentMana}>${Spell[${wardspell}].Mana}) {
			/varset nexthealspell ${wardspell}
			/varcalc hottimer ${Spell[${wardspell}].Duration.TotalSeconds}*${DurationMod}*10
		} else /if ((${Me.SpellReady[${bigheal1}]}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${bighealpoint}) && ${Me.CurrentMana}>${Spell[${bigheal1}].Mana} && ${NoCures}!=1) {
			/varset nexthealspell ${bigheal1}
		} else /if ((${Me.SpellReady[${bigheal2}]}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${bighealpoint}) && ${Me.CurrentMana}>${Spell[${bigheal2}].Mana} && ${NoCures}!=1) {
			/varset nexthealspell ${bigheal2}
		} else /if ((${Spawn[NPC los ${Me.XTarget[1].Name}].Distance}<150) && (${Me.SpellReady[${healnuke1}]}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${healpoint}) && (${Me.XTarget[1].PctHPs}<90) && (${Me.XTarget[1].PctHPs}>10) && ${Me.PctAggro}<50 && (${Me.PctMana}>=${Manatonuke}) && ${Me.CurrentMana}>${Spell[${healnuke1}].Mana}) {
			/varset nexthealspell ${healnuke1}
		} else /if ((${Spawn[NPC los ${Me.XTarget[1].Name}].Distance}<150) && (${Me.SpellReady[${healnuke2}]}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${healpoint}) && (${Me.XTarget[1].PctHPs}<90) && (${Me.XTarget[1].PctHPs}>10) && ${Me.PctAggro}<50 && (${Me.PctMana}>=${Manatonuke}) && ${Me.CurrentMana}>${Spell[${healnuke2}].Mana}) {
			/varset nexthealspell ${healnuke2}
		} else /if ((${Me.SpellReady[${nukeheal1}]}) && (${Spawn[NPC los ${Me.XTarget[1].Name}].Distance}<150) && (${Me.XTarget[1].PctHPs}<90) && (${Me.XTarget[1].PctHPs}>10) && ${Me.PctAggro}<50 && (${Me.PctMana}>=${Manatonuke}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${healpoint}) && ${Me.CurrentMana}>${Spell[${nukeheal1}].Mana}) {
			/call SpellQueue "${nukeheal1}" ${Me.XTarget[1].ID}
		} else /if ((${Me.SpellReady[${nukeheal2}]}) && (${Spawn[NPC los ${Me.XTarget[1].Name}].Distance}<150) && (${Me.XTarget[1].PctHPs}<90) && (${Me.XTarget[1].PctHPs}>10) && ${Me.PctAggro}<50 && (${Me.PctMana}>=${Manatonuke}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${healpoint}) && ${Me.CurrentMana}>${Spell[${nukeheal2}].Mana}) {
			/call SpellQueue "${nukeheal2}" ${Me.XTarget[1].ID}
		} else /if ((${Me.SpellReady[${remedyheal1}]}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${healpoint}) && !${remedyheal1.Equal[Complete Heal]} && ${Me.CurrentMana}>${Spell[${remedyheal1}].Mana}) {
			/varset nexthealspell ${remedyheal1}
		} else /if ((${Me.SpellReady[${remedyheal1}]}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${healpoint}) && ${remedyheal1.Equal[Complete Heal]} && ${Select[${Me.XTarget[${worsthurtname}].Class.ShortName},WAR,SHD,PAL]}>0 && ${Me.CurrentMana}>${Spell[${remedyheal1}].Mana}) {
			/varset nexthealspell ${remedyheal1}
		} else /if ((${Me.SpellReady[${remedyheal2}]}) && (${Me.XTarget[${worsthurtname}].PctHPs}<=${healpoint}) && ${Me.CurrentMana}>${Spell[${remedyheal2}].Mana}) {
			/varset nexthealspell ${remedyheal2}
		}
		/call SpellQueue "${nexthealspell}" ${worsthurtid}
	}
/return
|----------------------------------------------------------------------------
|- SUB: HealGroup
|---------------------------------------------------------------------------- 
Sub HealGroup
	/declare petworsthurtid		int local 0
	/declare petworsthp 		int local 100
	/declare worsthp 			int local 100 
	/declare worsthurtid		int local 0
	/declare worsthurtname		string local NULL
	/declare gmember			int local 0
	/declare HurtMember     	int local 0
	/varset GrPCond .
	/if (${Me.PctHPs}<=${grouphealpoint}) {
		/varcalc HurtMember ${HurtMember}+1
		/varset worsthurtid ${Me.ID}
		/varset worsthurtname ${Me.Name}
		/varset worsthp ${Me.PctHPs}
	}
   /for gmember 1 to ${Group}
		/if (${Group.Member[${gmember}].ID}==${qmtarget} && ${Me.PctMana}>60 && ${Group.Member[${gmember}].PctMana}<50 && ${Me.AltAbilityReady[Quiet Miracle]} && ${Group} && (!${Group.Member[${gmember}].Hovering} || ${Group.Member[${gmember}].Type.NotEqual[Corpse]})) /call AANow 135 ${Group.Member[${gmember}].ID}
		/if (${Group.Member[${gmember}].Distance}<=${healspellrange} && ${Group.Member[${gmember}].PctHPs}<100 && !${Group.Member[${gmember}].Hovering} && ${Group.Member[${gmember}].Type.NotEqual[Corpse]}) {
			/if ((${Group.Member[${gmember}].Distance}<100) && (${Group.Member[${gmember}].PctHPs}<${grouphealpoint}) && (${Group.Member[${gmember}].PctHPs}>=1)) /varcalc HurtMember ${HurtMember}+1
			/if (${Group.Member[${gmember}].PctHPs}<=${worsthp} && ((${Me.XTarget[1].PctHPs}<=99) || ${Group.Member[${gmember}].Distance}<50)) {
				/varset worsthurtid ${Group.Member[${gmember}].ID}
				/varset worsthurtname ${Group.Member[${gmember}].Name}
				/varset worsthp ${Group.Member[${gmember}].PctHPs}
				/varset GrPCond .
			}
         }
		/if (${Group.Member[${gmember}].Pet.ID}) {
			 /if (${Group.Member[${gmember}].Pet.Distance}<=${healspellrange} && (${Group.Member[${gmember}].Pet.PctHPs}<=${worsthp}) && (${Group.Member[${gmember}].Pet.PctHPs}<=${pethealpoint}) && ${pethealpoint}>0 && (${worsthp}>${grouphealpoint})) {         
			   /varset worsthurtid ${Group.Member[${gmember}].Pet.ID}
			   /varset worsthurtname ${Group.Member[${gmember}].Name}
			   /varset petworsthp ${Group.Member[${gmember}].Pet.PctHPs}
			   /varset GrPCond .Pet.
			 }
		  }
   /next gmember
|-Group Healing
    /if (${HurtMember}>=${grouphealcount} && ${Me.AltAbilityReady[Beacon of Life]} && ${Group}) {
		/call AANow ${Me.AltAbility[Beacon of Life].ID} ${Me.ID}
	} else /if (${HurtMember}>=${grouphealcount} && ${Me.SpellReady[${groupfastheal}]}) {
		/call SpellQueue "${groupfastheal}" ${Me.ID}
	} else /if (${HurtMember}>=${grouphealcount} && ${Me.SpellReady[${groupheal}]} && ${NoCures}!=1) {
		/call SpellQueue "${groupheal}" ${Me.ID}
	} else /if (${HurtMember}>=${grouphealcount} && ${Me.SpellReady[${grouphealnocure}]} && ${NoCures}==1) {
		/call SpellQueue "${grouphealnocure}" ${Me.ID}
	} else /if (${Me.PctHPs}<=${grouphealpoint}) {
		/varset healpoint ${grouphealpoint}
		/call HealQueue ${Me.ID}
	} else /if ((${usepromheals}==1) && (${PromisedTimer}==0) && (${Group.Member[${assistname}]${TnKCond}Distance}<=${promhealrange}) && ${Me.XTarget[1].ID} && ${Group.Member[${assistname}]${TnKCond}PctHPs}>=1 && ${Me.GemTimer[${promheal}]}==0 && (${Group.Member[${assistname}]${TnKCond}PctHPs}>${bighealpoint})) {
		/call SpellQueue "${promheal}" ${assistid}
		/if ((${Macro.Return.Equal[CAST_SUCCESS]}) || (${Macro.Return.Equal[CAST_TAKEHOLD]})) /varset PromisedTimer 21s
	} else /if ((${Group.Member[${assistname}]${TnKCond}Distance}<=${healspellrange}) && (${Group.Member[${assistname}]${TnKCond}PctHPs}<=${tankhealpoint}) && ${Group.Member[${assistname}]${TnKCond}PctHPs}>=1) {
		/varset healpoint ${tankhealpoint}
		/call HealQueue ${assistid}
	} else /if ((${Group.Member[${worsthurtname}]${GrPCond}Distance}<=${healspellrange}) && (${worsthp}<=${grouphealpoint} || ${petworsthp}<=${grouphealpoint}) && ${Group.Member[${worsthurtname}]${GrPCond}PctHPs}>=1) {
		/varset healpoint ${grouphealpoint}
		/call HealQueue ${worsthurtid}
	} else /if ((${Me.Corrupted.ID} || ${Me.Snared.ID}) && ${NoCures}!=1 && (${Me.AltAbilityReady[Purify Soul]} || ${Me.AltAbilityReady[Group Purify Soul ]} || ${Me.AltAbilityReady[Purified Spirits]}) && ${CureTimer}==0 && !${Me.Casting.ID} && ${CureOther}) {
		/call OtherCure ${Me.ID}
	} else /if (${Target.Type.Equal[PC]} && ${Target.BuffsPopulated}==TRUE && ${NoCures}!=1 && (${Target.Corrupted.ID} || ${Target.Snared.ID} || ${Target.Mezzed.ID} || ${Target.Charmed.ID}) && (${Me.AltAbilityReady[Purify Soul]} || ${Me.AltAbilityReady[Group Purify Soul ]}) && ${CureTimer}==0 && !${Me.Casting.ID} && ${CureOther}) {
		/call OtherCure ${Target.ID}
	} else /if ((${Me.Poisoned.ID} || ${Me.Diseased.ID} || ${Me.Cursed.ID}) && ${NoCures}!=1 && (${Me.AltAbilityReady[Radiant Cure]} || ${Me.AltAbilityReady[Purify Soul]} || ${Me.AltAbilityReady[Group Purify Soul ]} || ${Me.AltAbilityReady[Purified Spirits]}) && ${CureTimer}==0 && !${Me.Casting.ID}) {
		/call GroupCure ${Me.ID}
	} else /if (${Target.Type.Equal[PC]} && ${Target.BuffsPopulated}==TRUE && ${NoCures}!=1 && (${Target.Poisoned.ID} || ${Target.Diseased.ID} || ${Target.Cursed.ID}) && (${Me.AltAbilityReady[Radiant Cure]} || ${Me.AltAbilityReady[Purify Soul]} || ${Me.AltAbilityReady[Group Purify Soul ]}) && ${CureTimer}==0 && !${Me.Casting.ID}) {
		/call GroupCure ${Target.ID}
	} else /if (${Target.Buff[Gift of Living Death].ID} && ${CureTimer}==0 && !${Me.Casting.ID}) {
		/call SingleCure ${Target.ID}
	} else /if ((${DivineBuffOn}==1 )&& (${DivineBuffRecast}==1) && (!${Me.Casting.ID}) && (${Spell[${DivineBuff}].Range}>${Group.Member[${assistname}]${TnKCond}Distance}) && (${Group.Member[${assistname}]${TnKCond}PctHPs}>=1) && (!${Group.Member[${assistname}]${TnKCond}Hovering}) && (${Group.Member[${assistname}]${TnKCond}PctHPs}>${tankhealpoint}) && ${bufftimer}==0) {
			/call DivineUsage
	} else /if ((${Me.PctMana}>=${Manatonuke}) && (${Me.XTarget[1].ID}) && (!${Raid.Members}) && (${UseMelee}==1 || ${UseReverseDS}==1)) {
		/call Offense
	} else /if (${CureCount} && ${CureID}) {
		/call GroupCure ${CureID}
	}
	/varset GrPCond .
/return
|----------------------------------------------------------------------------
|- SUB: MedTime
|----------------------------------------------------------------------------
Sub MedTime
	/doevents
	/if (${Me.AltAbilityReady[Veturika's Perseverance]} && ${Me.PctMana}<=60 && ${Select[${Zone.ID},344,345,202]}==0 && ${Me.XTarget[1].ID}) {
		/call AANow ${Me.AltAbility[Veturika's Perseverance].ID}
	}
	/if (${UseModRods}) /call ClickModRoD
|-	/if (${FindItem[Transvergence].ID} && ${FindItem[Transvergence].Timer}==0 && ${Me.PctMana}<=50 && ${Me.XTarget[1].ID}) /useitem "${FindItem[Transvergence]}"
|-	/if (${FindItem[Modulation].ID} && ${FindItem[Modulation].Timer}==0 && ${Me.PctMana}<=50 && ${Me.XTarget[1].ID}) /useitem "${FindItem[Modulation]}"
	/if (${DoMed}) /call AutoMed
	/if (${enablesit}==0 && !${Me.Mount.ID} && ${Zone.Outdoor} && ${FindItem[${mountitem}].ID} && !${Me.XTarget[1].ID} && !${MoveTo.Moving} && !${AdvPath.Following} && ${Stick.Status.Equal[OFF]} && !${Me.FeetWet} && !${Me.Invis} && ${UseMelee}!=1) {
		/call ItemNow "${mountitem}"
	} else /if (${enablesit}==0 && (${Me.XTarget[1].ID} || ${Me.CombatState.Equal[DEBUFFED]}) && (${Me.AltAbilityReady[489]}) && (!${Me.Mount.ID}) && (!${Me.Buff[${Me.AltAbility[489].Spell}].ID}) && (${Spell[${Me.AltAbility[489].Spell}].Stacks})) {
		/call AANow 489
	} else /if (${enablesit}==0 && (${GoMana}==0) && (${Me.XTarget[1].ID} || ${Me.CombatState.Equal[DEBUFFED]}) && (!${Me.Mount.ID}) && (${Me.SpellReady[${yaulpspell}]}) && (!${Me.Buff[${yaulpspell}].ID}) && (${Spell[${yaulpspell}].Stacks})) {
		/cast "${yaulpspell}"
		/delay 2s !${Me.Casting.ID}
	}
/return
|----------------------------------------------------------------------------
|- SUB: Offensive Sub
|----------------------------------------------------------------------------
Sub Offense
	/declare tarstring string local
	/varset tarstring ${Me.XTarget[1].Name}
	/call FindTarget
	/if (${UseMelee}) /call EngageTarget
	/if ((${Spawn[NPC los ${Me.XTarget[1].Name}].Distance}<150) && (${Me.PctHPs}>${grouphealpoint}) && (${Spawn[NPC id ${autotargetid}].PctHPs}<${AutoAssistAt}) && (${Spawn[NPC id ${autotargetid}].PctHPs}>10) && ${Me.PctAggro}<70) {
		/if ((${reversenpcid}!=${autotargetid}) && (${Me.SpellReady[${ReverseDS}]}) && (${Spawn[${Me.XTarget[1].Name}].PctHPs}>=70) && ${UseReverseDS}) {
			/if (${ReverseDS.Find[Retort]}) {
				/call SpellQueue "${ReverseDS}" ${assistid}
			} else {
				/call SpellQueue "${ReverseDS}" ${autotargetid}
			}
			/if ((${Macro.Return.Equal[CAST_SUCCESS]}) || (${Macro.Return.Equal[CAST_TAKEHOLD]})) /varset reversenpcid ${autotargetid}
		} else /if ((${Me.AltAbilityReady[Turn Undead]}) && (${Spawn[NPC id ${autotargetid}].Distance}<200) && (${tarstring.Find[ndead]}>=2) && (${Me.AltAbility[Turn Undead]})) {
			/call AANow ${Me.AltAbility[Turn Undead].ID} ${autotargetid}}
		} else /if (${Group.Member[${assistname}].PctHPs}>${healpoint} && (${Me.SpellReady[${stunnuke}]}) && (${Spawn[NPC id ${autotargetid}].Distance}<${Spell[${stunnuke}].Range}) && ${Spawn[NPC id ${autotargetid}].Level}<=${StunLevel}) {
			/call SpellQueue "${stunnuke}" ${autotargetid}
		}		
	}
/return
|-------------------------------------------------------------------------------------
|- SUB: Rez Check
|-------------------------------------------------------------------------------------
Sub RezCheck    
    /declare RezID int local
	/declare RezName string local

	/hidecorpse off
	/if (${reztimer}==0 && ${Alert[3].Size}>0) /alert clear 3
	|-Will not rez in Guild lobby - Guild Hall - Plane of Knowledge
    /if (${Spawn[pccorpse noalert 3 group radius 100 zradius 50].ID}) {
		/varset RezID ${Spawn[pccorpse noalert 3 group radius 100 zradius 50].ID}
		/varset RezName ${Spawn[pccorpse noalert 3 group radius 100 zradius 50].Name}
		/target id ${RezID}
		/delay 5 ${Target.ID}==${RezID}
		/if (${Target.Distance}>25) /corpse
		/delay 5 ${Target.Distance}<25
		/if (${Me.AltAbilityReady[Blessing of Resurrection]}) {
			/docommand /dgt \ag Rezzing ${RezName}
			/call AANow 3800 ${RezID}
			/delay 4s !${Me.Casting.ID}
			/alert add 3 id ${RezID}
			/varset reztimer ${RetryRezDelay}s
		} else /if ((${FindItem[${epicwand}].ID}) && (${FindItem[${epicwand}].Timer}==0) && !${Me.AltAbility[3800]}) {
			/docommand /dgt \ag Rezzing ${RezName}
			/cast item "${epicwand}"
			/delay 10s !${Me.Casting.ID}
			/alert add 3 id ${RezID}
			/varset reztimer ${RetryRezDelay}s
		} else /if (${Me.Book[Reviviscence]} && ${Me.CurrentMana}>${Spell[Reviviscence].Mana} && !${Me.AltAbility[Blessing of Resurrection]}) {
			/docommand /dgt \ag Rezzing ${RezName}
			/call SpellQueue "Reviviscence" ${RezID}
			/alert add 3 id ${RezID}
			/varset reztimer ${RetryRezDelay}s
		}
	} else /if (${Spawn[pccorpse ${Me} noalert 3 radius 100 zradius 50].ID}) {
		/varset RezID ${Spawn[pccorpse ${Me} noalert 3 radius 100 zradius 50].ID}
		/varset RezName ${Spawn[pccorpse noalert 3 group radius 100 zradius 50].Name}
		/target id ${RezID}
		/delay 5 ${Target.ID}==${RezID}
		/if (${Target.Distance}>25) /corpse
		/delay 5 ${Target.Distance}<25
		/if (${Me.AltAbilityReady[Blessing of Resurrection]}) {
			/docommand /dgt \ag Battle rezzing ${RezName}
			/call AANow 3800 ${RezID}
			/delay 4s !${Me.Casting.ID}
			/alert add 3 id ${RezID}
			/varset reztimer ${RetryRezDelay}s
		} else /if ((${FindItem[${epicwand}].ID}) && (${FindItem[${epicwand}].Timer}==0) && !${Me.AltAbility[3800]}) {
			/docommand /dgt \ag Battle rezzing ${RezName}
			/cast item "${epicwand}"
			/delay 10s !${Me.Casting.ID}
			/alert add 3 id ${RezID}
			/varset reztimer ${RetryRezDelay}s
		} else /if (${Me.Book[Reviviscence]} && ${Me.CurrentMana}>${Spell[Reviviscence].Mana} && !${Me.AltAbility[Blessing of Resurrection]}) {
			/docommand /dgt \ag Battle rezzing ${RezName}
			/call SpellQueue "Reviviscence" ${RezID}
			/alert add 3 id ${RezID}
			/varset reztimer ${RetryRezDelay}s
		}
	}
	/hidecorpse npc
/return
|----------------------------------------------------------------------------
|- SUB: BuffCheck
|----------------------------------------------------------------------------
Sub BuffCheckOLD
	/if (${Me.Buff[Spiritual Vi].ID}) /blockspell add me ${Spell[${Me.Buff[Spiritual Vi]}].ID}
	/if (${Me.Buff[Focus].ID}) /blockspell add me ${Spell[${Me.Buff[Focus]}].ID}
	/if (!${Me.Buff[${SelfBuff}].ID} && !${Me.Casting.ID} && ${Spell[${SelfBuff}].Stacks} && ${usedruid}==0 && ${Me.Book[${SelfBuff}]} && ${Me.CurrentMana}>${Spell[${SelfBuff}].Mana}) {
			/call SpellQueue "${SelfBuff}" ${Me.ID}
		} else /if (!${Me.Buff[${GroupHealProcBuff}].ID} && !${Me.Casting.ID} && ${Spell[${GroupHealProcBuff}].Stacks} && ${Me.Book[${GroupHealProcBuff}]} && ${Me.CurrentMana}>${Spell[${GroupHealProcBuff}].Mana}) {
			/call SpellQueue "${GroupHealProcBuff}" ${Me.ID}
		} else /if (!${Me.Aego.ID} && !${Me.Skin.ID} && !${Me.Symbol.ID} && ${Me.Book[${GroupBuff}]} && !${Spell[${Me.Buff[${GroupBuff}]}].ID} && ${Me.CurrentMana}>${Spell[${GroupBuff}].Mana}) {
			/call SpellQueue "${GroupBuff}" ${Me.ID}
		} else /if (${GroupBuffRecast}==1 && ${Me.Book[${GroupBuff}]} && ${bufftimer}==0 && ${Me.CurrentMana}>${Spell[${GroupBuff}].Mana}) {
			/call SpellQueue "${GroupBuff}" ${assistid}
			/varset GroupBuffRecast 0
		} else /if (${TankBuffRecast}==1 && !${Me.Casting.ID} && ${Spell[${TankBuff}].Range}>${Group.Member[${assistname}].Distance} && !${Spawn[${assistname}].Hovering} && ${Me.Book[${TankBuff}]} && ${bufftimer}==0 && ${Me.CurrentMana}>${Spell[${TankBuff}].Mana}) {
			/call SpellQueue "${TankBuff}" ${assistid}
			/varset TankBuffRecast 0
		} else /if (!${Me.Casting.ID} && ${Spell[${SymbolBuff}].Range}>${Spawn[${SymbolBuffRecast}].Distance} && ${Spawn[${SymbolBuffRecast}].ID} && !${Spawn[${SymbolBuffRecast}].Hovering} && ${Me.Book[${SymbolBuff}]} && ${Me.CurrentMana}>${Spell[${SymbolBuff}].Mana} && ${bufftimer}==0) {
			/call SpellQueue "${SymbolBuff}" ${Spawn[${SymbolBuffRecast}].ID}
			/varset SymbolBuffRecast 0
		} else /if (!${Me.Casting.ID} && ${Spell[${HPBuff}].Range}>${Spawn[${HPBuffRecast}].Distance} && ${Spawn[${HPBuffRecast}].ID} && !${Spawn[${HPBuffRecast}].Hovering} && ${Me.Book[${HPBuff}]} && ${Me.CurrentMana}>${Spell[${HPBuff}].Mana} && ${bufftimer}==0) {
			/call SpellQueue "${HPBuff}" ${Spawn[${HPBuffRecast}].ID}
			/varset HPBuffRecast 0
		} else /if (!${Me.Casting.ID} && ${Spell[${TankBuff}].Range}>${Spawn[${ShiningBuffRecast}].Distance} && ${Spawn[${ShiningBuffRecast}].ID} && !${Spawn[${ShiningBuffRecast}].Hovering} && ${Me.Book[${TankBuff}]} && ${Me.CurrentMana}>${Spell[${TankBuff}].Mana} && ${bufftimer}==0) {
			/call SpellQueue "${TankBuff}" ${Spawn[${ShiningBuffRecast}].ID}
			/varset ShiningBuffRecast 0
		} else /if (${DivineBuffOn}==1 && ${DivineBuffRecast}==1 && !${Me.Casting.ID} && ${Spell[${DivineBuff}].Range}>${Group.Member[${assistname}].Distance} && ${Group.Member[${assistname}].PctHPs}>=1 && !${Group.Member[${assistname}].Hovering} && ${Me.Book[${DivineBuff}]} && ${bufftimer}==0 && ${Me.CurrentMana}>${Spell[${DivineBuff}].Mana}) {
			/call DivineUsage
			/varset DivineBuffRecast 0
		} else /if (${Bool[${aurabuff1}]} && (!${Me.Casting.ID}) && !${Me.Aura[1].Name.Find[${cleanaura1}]} && !${Me.Aura[2].Name.Find[${cleanaura1}]} && ${Me.Book[${aurabuff1}]} && ${Me.CurrentMana}>${Spell[${aurabuff1}].Mana}) {
			/call SpellQueue "${aurabuff1}" ${Me.ID}
		} else /if (${Bool[${aurabuff2}]} && (!${Me.Casting.ID}) && !${Me.Aura[1].Name.Find[${cleanaura2}]} && !${Me.Aura[2].Name.Find[${cleanaura2}]} && ${Me.Book[${aurabuff2}]} && ${Me.CurrentMana}>${Spell[${aurabuff2}].Mana}) {
			/call SpellQueue "${aurabuff2}" ${Me.ID}
		}
/return	
|----------------------------------------------------------------------------
|- SUB: BuffCheck
|----------------------------------------------------------------------------
Sub BuffCheck
	/if (${Me.Buff[Spiritual Vi].ID}) /blockspell add me ${Spell[${Me.Buff[Spiritual Vi]}].ID}
	/if (${Me.Buff[Focus].ID}) /blockspell add me ${Spell[${Me.Buff[Focus]}].ID}
	/if (!${Me.Buff[${SelfBuff}].ID} && !${Me.Casting.ID} && ${Spell[${SelfBuff}].Stacks} && ${usedruid}==0 && ${Me.Book[${SelfBuff}]} && ${Me.CurrentMana}>${Spell[${SelfBuff}].Mana}) {
		/call SpellQueue "${SelfBuff}" ${Me.ID}
	} else /if (!${Me.Buff[${GroupHealProcBuff}].ID} && !${Me.Casting.ID} && ${Spell[${GroupHealProcBuff}].Stacks} && ${Me.Book[${GroupHealProcBuff}]} && ${Me.CurrentMana}>${Spell[${GroupHealProcBuff}].Mana}) {
		/call SpellQueue "${GroupHealProcBuff}" ${Me.ID}
	} else /if (!${Me.Aego.ID} && !${Me.Skin.ID} && !${Me.Symbol.ID} && ${Me.Book[${GroupBuff}]} && !${Spell[${Me.Buff[${GroupBuff}]}].ID} && ${Me.CurrentMana}>${Spell[${GroupBuff}].Mana}) {
		/call SpellQueue "${GroupBuff}" ${Me.ID}
	} else /if (!${Me.Casting.ID} && ${Spell[${SymbolBuff}].Range}>${Spawn[${SymbolBuffRecast}].Distance} && ${Spawn[${SymbolBuffRecast}].ID} && !${Spawn[${SymbolBuffRecast}].Hovering} && ${Me.Book[${SymbolBuff}]} && ${Me.CurrentMana}>${Spell[${SymbolBuff}].Mana} && ${bufftimer}==0) {
		/call SpellQueue "${SymbolBuff}" ${Spawn[${SymbolBuffRecast}].ID}
		/varset SymbolBuffRecast 0
	} else /if (!${Me.Casting.ID} && ${Spell[${HPBuff}].Range}>${Spawn[${HPBuffRecast}].Distance} && ${Spawn[${HPBuffRecast}].ID} && !${Spawn[${HPBuffRecast}].Hovering} && ${Me.Book[${HPBuff}]} && ${Me.CurrentMana}>${Spell[${HPBuff}].Mana} && ${bufftimer}==0) {
		/call SpellQueue "${HPBuff}" ${Spawn[${HPBuffRecast}].ID}
		/varset HPBuffRecast 0
	} else /if (!${Me.Casting.ID} && ${Spell[${TankBuff}].Range}>${Spawn[${ShiningBuffRecast}].Distance} && ${Spawn[${ShiningBuffRecast}].ID} && !${Spawn[${ShiningBuffRecast}].Hovering} && ${Me.Book[${TankBuff}]} && ${Me.CurrentMana}>${Spell[${TankBuff}].Mana} && ${bufftimer}==0) {
		/call SpellQueue "${TankBuff}" ${Spawn[${ShiningBuffRecast}].ID}
		/varset ShiningBuffRecast 0
	} else /if (${Bool[${aurabuff1}]} && (!${Me.Casting.ID}) && !${Me.Aura[1].Name.Find[${cleanaura1}]} && !${Me.Aura[2].Name.Find[${cleanaura1}]} && ${Me.Book[${aurabuff1}]} && ${Me.CurrentMana}>${Spell[${aurabuff1}].Mana}) {
		/call SpellQueue "${aurabuff1}" ${Me.ID}
	} else /if (${Bool[${aurabuff2}]} && (!${Me.Casting.ID}) && !${Me.Aura[1].Name.Find[${cleanaura2}]} && !${Me.Aura[2].Name.Find[${cleanaura2}]} && ${Me.Book[${aurabuff2}]} && ${Me.CurrentMana}>${Spell[${aurabuff2}].Mana}) {
		/call SpellQueue "${aurabuff2}" ${Me.ID}
	}
/return	
|----------------------------------------------------------------------------
|- SUB: DA Logic 
|----------------------------------------------------------------------------
Sub DALogic
	/if (${Me.Song["Touch of the Divine"].ID}) {
		/echo Touch of the Divine detected!
		/removebuff Touch of the Divine
		/delay 5 !${Me.Song["Touch of the Divine"].ID}
    }
	/call AggroCheck
/return
|----------------------------------------------------------------------------
|- SUB: Aggro Check
|----------------------------------------------------------------------------
Sub AggroCheck
	/if (${Me.AltAbilityReady[Divine Peace]}) {
		/call AANow ${Me.AltAbility[Divine Peace].ID}
	} else /if (${Me.AltAbilityReady[Sanctuary]}) {
		/call AANow ${Me.AltAbility[Sanctuary].ID}
	} else /if (${Me.AltAbilityReady[Silent Casting]}) {
		/call AANow ${Me.AltAbility[Silent Casting].ID}
	}
/return
|----------------------------------------------------------------------------
|- SUB: Misc Events
|----------------------------------------------------------------------------
Sub Event_GoMana
	/varset GoMana 1
	/timed 180 /varset GoMana 0
	/call Healgroup
/return

Sub Event_GoMoff
/varset GoMana 0
/return
|----------------------------------------------------------------------------
|- SUB: Symbol - HP Buff Single use
|----------------------------------------------------------------------------
Sub Event_Chat(string ChatType,string ChatSender,string ChatText)
	/if (${Spawn[pc ${ChatSender}].ID} && ${ChatType.Equal[tell]}) /mqlog ${ChatSender} -- ${ChatText}
	/if (${Spawn[${ChatSender.Right[-2].Left[-1]}].ID}) {
		/if (${Select[${ChatText},symbol,Symbol,SYMBOL]} && (${Spawn[${ChatSender.Right[-2].Left[-1]}].Distance}<100)) /varset SymbolBuffRecast ${Spawn[${ChatSender.Right[-2].Left[-1]}].ID}
		/if (${Select[${ChatText},hpbuff,HPBuff,HPBUFF,HpBuff,Hpbuff,HPbuff]} && (${Spawn[${ChatSender.Right[-2].Left[-1]}].Distance}<100)) /varset HPBuffRecast ${Spawn[${ChatSender.Right[-2].Left[-1]}].ID}
		/if (${Select[${ChatText},Shining,shining]} && (${Spawn[${ChatSender.Right[-2].Left[-1]}].Distance}<100)) /varset ShiningBuffRecast ${Spawn[${ChatSender.Right[-2].Left[-1]}].ID}
		/if (${Select[${ChatText},Cure,cure,CURE]} && (${Spawn[${ChatSender.Right[-2].Left[-1]}].Distance}<200)) /call SingleCure ${Spawn[${ChatSender.Right[-2].Left[-1]}].ID}
		/if (${Select[${ChatText},RC,Radiant,GC]} && (${Spawn[${ChatSender.Right[-2].Left[-1]}].Distance}<200)) /call GroupCure ${Spawn[${ChatSender.Right[-2].Left[-1]}].ID}
	}
/return
|----------------------------------------------------------------------------
|- SUB: SingleCure
|----------------------------------------------------------------------------
Sub SingleCure(int cureid)
	/varset CureTimer 10s
	/if ((${Me.SpellReady[${bigheal1}]}) && ${Me.CurrentMana}>${Spell[${bigheal1}].Mana}) {
		/call SpellQueue "${bigheal1}" ${cureid}
	} else /if ((${Me.SpellReady[${bigheal2}]}) && ${Me.CurrentMana}>${Spell[${bigheal2}].Mana}) {
		/call SpellQueue "${bigheal2}" ${cureid}
	} else /if (${Me.AltAbilityReady[Purify Soul]} && !${Me.Casting.ID}) {
		/call AANow ${Me.AltAbility[Purify Soul].ID} ${cureid}
	}
/return
|----------------------------------------------------------------------------
|- SUB: GroupCure
|----------------------------------------------------------------------------
Sub GroupCure(int cureid)
	/varset CureTimer 10s
	/if (${Me.AltAbilityReady[Radiant Cure]}) {
		/call AANow ${Me.AltAbility[Radiant Cure].ID} ${cureid}
	} else /if (${Me.AltAbilityReady[Group Purify Soul]}) {
		/call AANow ${Me.AltAbility[Group Purify Soul].ID} ${cureid}
	} else /if (${Me.AltAbilityReady[Purify Soul]} && !${Me.Casting.ID}) {
		/call AANow ${Me.AltAbility[Purify Soul].ID} ${cureid}
	}
	/varset CureCount 0
	/varset CureID 0
/return
|----------------------------------------------------------------------------
|- SUB: OtherCure - Used for curing Corruption, Snare and Mez
|----------------------------------------------------------------------------
Sub OtherCure(int cureid)
	/varset CureTimer 10s
	/if (${Me.ID}==${cureid} && ${Me.AltAbilityReady[Purified Spirits]}) {
		/call AANow ${Me.AltAbility[Purified Spirits].ID} ${cureid}
	} else /if (${Me.AltAbilityReady[Group Purify Soul]}) {
		/call AANow ${Me.AltAbility[Group Purify Soul].ID} ${cureid}
	} else /if (${Me.AltAbilityReady[Purify Soul]} && !${Me.Casting.ID}) {
		/call AANow ${Me.AltAbility[Purify Soul].ID} ${cureid}
	}
/return
|----------------------------------------------------------------------------
|- SUB: DivineUsage
|----------------------------------------------------------------------------
Sub DivineUsage
	/varset Emeralds ${FindItem[=Emerald].StackCount}
	/varset Pearls ${FindItem[=Emerald].StackCount}
	/if ((${Emeralds}<2 && ${DivineBuff.NotEqual[Death Pact]}) || (${DivineBuff.Equal[Death Pact]} && ${Pearls}<2)) {
		/echo Unable to cast ${DivineBuff} No reagent disabling Divine Casting
		/varset DivineBuffOn 0
		/varset changetoini 1
	} else {
		/call SpellQueue "${DivineBuff}" ${assistid}
		/if ((${Macro.Return.Equal[CAST_SUCCESS]})||(${Macro.Return.Equal[CAST_TAKEHOLD]})) /varset DivineBuffRecast 0
	}
/return
|----------------------------------------------------------------------------
|- SUB: Buff Event
|----------------------------------------------------------------------------
Sub Event_Buff(string line, string Mybuff)
    /if (${Mybuff.Equal[${TankBuffMatch}]})	/varset TankBuffRecast 1
	/if (${Mybuff.Equal[${DivineBuff}]}) 	/varset DivineBuffRecast 1
/return
|----------------------------------------------------------------------------
|- SUB: ReactCure - 
|----------------------------------------------------------------------------
Sub Event_ReactCure
	/call GroupCure
/return
|----------------------------------------------------------------------------
|- SUB: TankChangeCheck
|---------------------------------------------------------------------------- 
Sub TankChangeCheck
	/if (${PetTank} && ${Group.Member[${assistname}].Pet.ID}) {
		/docommand /varset tempid ${Group.Member[${assistname}].Pet.ID}
		/if (${tempid}!=${assistid}) {
			/echo Tank ID Mismatch - Fixing it
			/varset assistid ${tempid}
		}
	} else /if (!${PetTank} && ${Group.Member[${assistname}].ID}) {
		/docommand /varset tempid ${Group.Member[${assistname}].ID}
		/if (${tempid}!=${assistid}) {
			/echo Tank ID Mismatch - Fixing it
			/varset assistid ${tempid}
		}
	}
	/if (!${Spawn[${assistid}].PctHPs}>=1) {
		/if (!${Bool[${oldtank}]}) /varset oldtank ${assistname}
		/if (!${Bool[${oldtankid}]}) /varset oldtankid ${assistid}
		/if (${Spawn[group notid ${Me.ID} radius 150].ID}) {
			/echo Tank Gone - Switching to Nearest Group Member Alive
			/varset assistname ${Spawn[group notid ${Me.ID} radius 150]}
			/varset assistid ${Spawn[group notid ${Me.ID} radius 150].ID}
			/if (${Spawn[${assistid}].Type.Equal[Pet]}) {
				/varset TnKCond .Pet.
			} else {
				/varset TnKCond .
			}
		} else {
			/echo Tank and Group Gone - Switching to Self Heal mode
			/varset assistname ${Me.Name}
			/varset assistid ${Me.ID}
			/varset TnKCond .
		}
	}
	/if (${Group.Member[${oldtank}].ID}) {
		/if (!${PetTank}) {
			/varset assistname ${oldtank}
			/varset assistid ${Group.Member[${oldtank}].ID}
			/echo Tank is Back - Switching him back to maintank
			/varset oldtank NULL
			/varset oldtankid NULL
		} else /if (${PetTank} && ${Group.Member[${oldtank}].Pet.ID}) {
			/varset assistname ${oldtank}
			/varset assistid ${Group.Member[${oldtank}].Pet.ID}
			/echo Tank is Back - Switching him back to maintank
			/varset oldtank NULL
			/varset oldtankid NULL
		}
		/if (${Spawn[${assistid}].Type.Equal[Pet]}) {
			/varset TnKCond .Pet.
		} else {
			/varset TnKCond .
		}
	}
	/if (${newtank}==1) {
		/docommand /varset assistid ${Target.ID}
		/varset newtank 0
		/if (${Spawn[${assistid}].Type.Equal[Pet]}) {
			/docommand /varset assistname ${Target.Master.Name}
			/echo Main Tank set to ${Target.Name}
			/varset TnKCond .Pet.
			/varset PetTank 1
		} else {
			/docommand /varset assistname ${Target.Name}
			/echo Main Tank set to ${assistname}
			/varset TnKCond .
			/varset PetTank 0
		}
	}
/return 