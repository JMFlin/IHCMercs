| GTFO! 3.01 23 May 2018
| By Redbot & gSe7eN exclusively for RedGuides.com
| http://www.redguides.com/forums/showthread.php/25958-GTFO!-an-AFK-macro-to-avoid-player-petitions
| Requires MQ2Posse, MQ2Cast, MQ2AdvPath, better with MQ2Autoloot and MQ2Nav
|
| GTFO! is for those who want to kindly give up their camp to strangers, 
| automatically. When a stranger is within a certain radius, your group will kindly leave the area, either a
| druid/wizard port, some item of your choosing, Origin or some other aa, or any thing at all you can use
| to get to a new place. You can then either /exit, or automatically return to action (fellowship or some 
| other portback method required).
|
| Please read about MQ2Posse and edit MQ2Posse.ini before running this macro.
| (You have to type "/posse on" on each character for the MQ2Posse.ini to be created)
| http://www.redguides.com/forums/showthread.php/25075-MQ2Posse
|
| USAGE:
| Step 1) Run gtfo.mac on each character in each group. Edit the resulting gtfo.ini file, then save.
| Step 2) Open MQ2Posse.ini (in your mq2 root folder)
| 			Under [Name_Commands] enter:
| 			"0=/endmac" 
|			"1=/mac GTFO"
| 			Do this for each character.
| 
| To adjust the range:
| Step 1) edit mq2posse.ini and change Radius + ZRadius for each character
| Step 2) edit each character's kissassist.ini and set "AFKPCRadius". Make it the same as MQ2Posse's radius.
| All character's radius settings must match.
------------------------------------------------------------
Sub Main
	/declare MacroName      string outer GTFO
	/declare IniFileName      string outer GTFO.ini
	/declare ThisVersion		float outer 3.00
	/declare IniVersion			float outer ${Ini[${IniFileName},General,Version]}
	/if (${Ini[${IniFileName},${Me.CleanName},Class].NotEqual[${Me.Class}]}) /ini "${IniFileName}" "${Me.CleanName}" "Class" "${Me.Class}"
	/call LoadIni "${IniFileName}" LOAD
	/if (${DoIPort}) {
	/call Caster
	} else {
	/call Follower
	}
/return

Sub Caster
	/echo Right. So I'm supposed to do something here. Like port.
	| If this is a group port, I'm going to summon people to me.
	/if (${Spell[${PortHow}].TargetType.Find[group]}) {
		/if (${Plugin[MQ2Nav].Name.Length} && ${Navigation.MeshLoaded}) {
			/bcg //target id ${Me.ID}
			/bcg //nav id ${Me.ID}
		} else /if (${Bool[${Plugin[${MQ2MoveUtils}]}]}) {
			/bcg //stick id ${Me.ID}
		} else {
			/bcg //echo I need to learn how to use MQ2Nav or MQ2MoveUtils.
			/bcg //target id ${Me.ID}
			/bcg //follow
		}
	}			
	/delay 3s
	/stopcast
	/delay 2
	/echo I'm up out this joint by using ${PortHow}.
	/casting "${PortHow}" 
	/plugin mq2posse unload noauto
	/call Chillout
	:OnExit
		/if (${Select[${Me.Class.ShortName},ROG]} && ${Bool[${Plugin[MQ2Melee]}]}) /squelch /melee on sneak=on hide=on
/return

Sub Follower
	/if (${Select[${Me.Class},Bard]} && ${Bool[${Plugin[mq2twist]}]}) /stoptwist
	/stopcast
	/delay 1s
	/plugin mq2posse unload noauto
	/call Chillout
/return

sub Chillout
	| is GTFO running too often?
	/if (${Defined[gtforan]}) {
		/if (${gtforan} >= ${Retry}) {
		/echo GTFO is triggering too often. Let's call it a night.
		/call exit
		} else {
		/varcalc gtforan ${gtforan}+1
		}
	}
	/if (!${Defined[gtforan]}) {
		/declare gtforan int global 1
	}
	
	/delay 80s
	
	| Would I rather just exit?
	/if (!${KeepPlaying}) {
	/echo Looks like you have "KeepPlaying" set to false, so I'll call it a night.
	/call exit
	}
	

	| Has this character been killed recently?
	/if (${Me.Buff[Resurrection Sickness].ID} || ${Me.Buff[Revival Sickness].ID}) {
            /echo oh no I must have died recently! I'm quitting this macro.
            /call exit
          }
		  
	| Is everyone in the zone?
	/delay 5s
	/call Lonely
	| If I should sell things, I can do that now. In INI file, add VendorItems=TRUE to the settings section
	/if (${VendorItems}) /call VendorItems
	/echo taking a ${Sleep} minute break.  Type /endmac to play manually.
	/delay ${Sleep}m
	/echo ${Sleep} minute break over. Give me a minute to stretch. *hits snooze*
	|summoning pettank if needed
	/if (${Select[${Me.Class.ShortName},MAG,NEC,BST]} && !${PetSpell.Equal[NULL]} && !${Me.Pet.ID} && ${MainAssist.Equal[${Me}]}) {
		/echo summoning pet
		/casting "${PetSpell}" 
	}
	/if (${Select[${Me.Class.ShortName},ROG]} && ${Bool[${Plugin[MQ2Melee]}]}) /squelch /melee on sneak=off hide=off
	/delay 30s
	/if (${ReturnHow.Equal[Fellowship Registration Insignia]}) {
		/if (!${Me.Fellowship.Campfire}) {
			/echo There is no campfire up.  How are we supposed to stay warm?
			/call exit
			/return
		}
		/if (${Me.Fellowship.CampfireZone.ID}==${Zone.ID}) {
			/echo I am confused because I'm in the same zone as my campfire. I need an adult.
			/call exit
			/return
		}
		/if (${FindItem[Fellowship Registration Insignia].TimerReady} == 0) {
			/echo Time to get back to work. Clicking Fellowship Insignia.
			/makemevisible
			/delay 15
			/casting "Fellowship Registration Insignia" item
		} else {
			/echo Either I can't find my Fellowship Registration Insignia or the timer isn't ready.
			/call exit
		}
	} else {
		/casting "${ReturnHow}"
	}
	/delay 90s
	/call Lonely
	/echo reloading MQ2Posse to clear triggers in 60 seconds. Sorry for the error spam.
	/plugin mq2posse unload noauto
	/delay 4s
	/plugin mq2posse
	/delay 30s
	/varset gtforan 0
	/call FireMac
/return

sub FireMac
	/target ${GroupMainAssist}
	/delay 5s
	/if (${MacroOnReturn.Find[/]}) {
		/docommand ${MacroOnReturn}
	} else {
		/macro ${MacroOnReturn}
	}
/return
	
sub Lonely
	| Is everyone in the zone?
	/delay 5s
	/if (${Group.AnyoneMissing}) {
		/echo Someone didn't make it. I'm too Lonely to continue.
		/call exit
		/return
	}
	/return
	
sub VendorItems
	/if (${Spawn[merchant radius 50].ID}) {
		/echo Imma do bidness with ${Spawn[merchant radius 50].CleanName} real quick.
		/target ${Spawn[merchant radius 50]}
		/nav target id ${${Spawn[merchant radius 50].ID}
		| If MQ2Autoloot isn't loaded, see if it loads and if not GTFO of this sub routine
		/if (!${Plugin[MQ2AutoLoot].Name.Length}) /plugin MQ2AutoLoot noauto
		/if (!${Plugin[MQ2AutoLoot].Name.Length}) {
			/echo I can't do autoselling without MQ2AutoLoot. Are you my mommy?
			/return 
		}
		/autoloot sell
		}
/return

Sub LoadVar(IniSection,IniVar,IniValue,MacroVar,MyIni,Function,VarType)
	/if (!${Defined[${MacroVar}]} && ${Defined[VarType]}) /declare ${MacroVar} ${VarType} outer
	/if (${Function.Equal[LOAD]}) {
		/declare IniString string local ${Ini[${MyIni},${IniSection},${IniVar},NOTFOUND]}
		/varset ${MacroVar} ${IniString}
	}
	/if (${IniString.Equal["NOTFOUND"]} || ${Function.Equal[SAVE]}) {
		/if (${IniString.Equal["NOTFOUND"]} && ${Function.Equal[LOAD]}) /varset ${MacroVar} ${IniValue}
		/if (${IniString.Equal["NOTFOUND"]} && ${Function.Equal[LOAD]}) /varset ${MacroVar} ${IniValue}
		/ini ${MyIni} "${IniSection}" "${IniVar}" "${${MacroVar}}"
	}
/return

Sub LoadIni(MyIni,Function)
	/if (${Ini[${MyIni},General,Version].Equal[3.0]}) /call LoadVar General Version 3.0 Version "${MyIni}" SAVE string
	/call LoadVar ${Me.CleanName} MyClass "${Me.Class}" MyClass "${MyIni}" ${Function} string
	/call LoadVar ${Me.CleanName} DoIPort FALSE DoIPort "${MyIni}" ${Function} string
	/call LoadVar ${Me.CleanName} PortHow origin PortHow "${MyIni}" ${Function} string
	/call LoadVar ${Me.CleanName} KeepPlaying TRUE KeepPlaying "${MyIni}" ${Function} bool
	/call LoadVar ${Me.CleanName} VendorItems FALSE VendorItems "${MyIni}" ${Function} bool
	/call LoadVar ${Me.CleanName} Sleep 14 Sleep "${MyIni}" ${Function} int
	/call LoadVar ${Me.CleanName} ReturnHow "Fellowship Registration Insignia" ReturnHow "${MyIni}" ${Function} string
	/call LoadVar ${Me.CleanName} MacroOnReturn kissassist MacroOnReturn "${MyIni}" ${Function} string
	/if (${Select[${Me.Class.ShortName},MAG,NEC,BST]}) /call LoadVar ${Me.CleanName} PetSpell NULL PetSpell "${MyIni}" ${Function} string
	/call LoadVar ${Me.CleanName} GroupMainAssist ${Group.MainAssist} GroupMainAssist "${MyIni}" ${Function} string
	/call LoadVar ${Me.CleanName} Retry 4 Retry "${MyIni}" ${Function} int
/return
	
sub exit
	/echo Exiting in 30 seconds. To stay logged in type /endmac
	/delay 30s
	/g I've decided to GTFO for good. Bye bye cruel Norrath.
	/exit
/return
